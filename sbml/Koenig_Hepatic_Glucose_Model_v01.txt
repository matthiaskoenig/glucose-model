
// helper functions
function max(x,y)
  piecewise(x,gt(x,y),y)
end

function min(x,y)
  piecewise(x,lt(x,y),y) 
end


model *Koenig2014_hepatic_glucose()
  // Units
  unit substance = 1e-3 mole;
  unit volume = litre;
  unit area = metre^2; 
  unit length = metre;

  unit per_s = 1 / second;
  unit minute = 60 second;  
  unit mmol = 1e-3 mole;
  unit mM = mmol / litre;
  unit per_mM = 1 / mM;
  unit mMmM = mM^2;
  unit mmol_per_s = mmol / second;
  
  unit pmol = 1e-12 mole;
  unit pmol_per_l = pmol / litre;
  


  // Compartments and Species
  const compartment extern, cyto, mito;
  extern = 10 litre;
  cyto = 1 litre;
  mito = 5 litre;

  species $atp in cyto, $adp in cyto, $amp in cyto, utp in cyto, udp in cyto;
  species gtp in cyto, gdp in cyto, $nad in cyto, $nadh in cyto;
  species $phos in cyto, pp in cyto, $co2 in cyto, $h2o in cyto, $h in cyto;
  species glc1p in cyto, udpglc in cyto, glyglc in cyto, glc in cyto;
  species glc6p in cyto, fru6p in cyto, fru16bp in cyto, fru26bp in cyto;
  species grap in cyto, dhap in cyto, bpg13 in cyto, pg3 in cyto, pg2 in cyto;
  species pep in cyto, pyr in cyto, oaa in cyto, lac in cyto;

  species $glc_ext in extern, $lac_ext in extern;

  species $co2_mito in mito, $phos_mito in mito, oaa_mito in mito, pep_mito in mito;
  species $acoa_mito in mito, pyr_mito in mito, $cit_mito in mito;
  species $atp_mito in mito, $adp_mito in mito, gtp_mito in mito, gdp_mito in mito;
  species $coa_mito in mito, $nadh_mito in mito, $nad_mito in mito, $h2o_mito in mito, $h_mito in mito; 

 // Species initializations:
  atp = 2.8000 mM;
  adp = 0.8000 mM;
  amp = 0.1600 mM;
  utp = 0.2700 mM;
  udp = 0.0900 mM;
  gtp = 0.2900 mM;
  gdp = 0.1000 mM;
  nad = 1.2200 mM;
  nadh = 0.56E-3 mM;
  phos = 5.0000 mM;
  pp = 0.0080 mM;
  co2 = 5.0000 mM;
  h2o = 0 mM;
  h = 0 mM;
  glc1p = 0.0120 mM;
  udpglc = 0.3800 mM;
  glyglc = 250.0000 mM;
  glc = 5.0000 mM;
  glc6p = 0.1200 mM;
  fru6p = 0.0500 mM;
  fru16bp = 0.0200 mM;
  fru26bp = 0.0040 mM;
  grap = 0.1000 mM;
  dhap = 0.0300 mM;
  bpg13 = 0.3000 mM;
  pg3 = 0.2700 mM;
  pg2 = 0.0300 mM;
  pep = 0.1500 mM;
  pyr = 0.1000 mM;
  oaa = 0.0100 mM;
  lac = 0.5000 mM;
  
  glc_ext = 3.0000 mM;
  lac_ext = 1.2000 mM;
  
  co2_mito = 5.0000 mM;
  phos_mito = 5.0000 mM;
  oaa_mito = 0.0100 mM;
  pep_mito = 0.1500 mM;
  acoa_mito = 0.0400 mM;
  pyr_mito = 0.1000 mM;
  cit_mito = 0.3200 mM;
  atp_mito = 2.8000 mM;
  adp_mito = 0.8000 mM;
  gtp_mito = 0.2900 mM;
  gdp_mito = 0.1000 mM;
  coa_mito = 0.0550 mM;
  nadh_mito = 0.2400 mM;
  nad_mito = 0.9800 mM;
  h_mito   = 0 mM;
  h2o_mito = 0 mM;

  // Assignment rules for balances
  nadh_tot := nadh + nad;
  atp_tot := atp + adp + amp;
  utp_tot := utp + udp;
  gtp_tot := gtp + gdp;
  nadh_mito_tot := nadh_mito + nad_mito;
  atp_mito_tot := atp_mito + adp_mito;
  gtp_mito_tot := gtp_mito + gdp_mito; 

  nadh_tot has mM;
  atp_tot has mM;
  utp_tot has mM;
  gtp_tot has mM;
  nadh_mito_tot has mM;
  atp_mito_tot has mM;
  gtp_mito_tot has mM;

  // hormonal regulation
  x_ins1 = 818.9 pmol_per_l;
  x_ins2 = 0 pmol_per_l;
  x_ins3 = 8.6 mM;
  x_ins4 = 4.2 dimensionless;
  ins = max(0.0, (x_ins1-x_ins2) * glc_ext^x_ins4/(glc_ext^x_ins4 + x_ins3^x_ins4));
  ins has pmol_per_l;

  x_glu1 = 190 pmol_per_l;
  x_glu2 = 37.9 pmol_per_l;
  x_glu3 = 3.01 mM;
  x_glu4 = 6.40 dimensionless;
  glu = max(0.0, (x_glu1-x_glu2)*(1 - glc_ext^x_glu4/(glc_ext^x_glu4 + x_glu3^x_glu4)));
  glu has pmol_per_l;

  x_epi1 = 6090 pmol_per_l;
  x_epi2 = 100 pmol_per_l;
  x_epi3 = 3.10 mM;
  x_epi4 = 8.40 dimensionless;
  epi = max(0.0, (x_epi1-x_epi2) * (1 - glc_ext^x_epi4/(glc_ext^x_epi4 + x_epi3^x_glu4)));
  epi has pmol_per_l;

  K_val = 0.1 dimensionless;
  epi_f = 0.8 dimensionless;
  K_ins = (x_ins1-x_ins2) * K_val;
  K_glu = (x_glu1-x_glu2) * K_val;
  K_epi = (x_epi1-x_epi2) * K_val;
  gamma = 0.5 * (1 - ins/(ins+K_ins) + max(glu/(glu+K_glu), epi_f*epi/(epi+K_epi)) );
  K_ins has pmol_per_l;
  K_glu has pmol_per_l;
  K_epi has pmol_per_l;
  gamma has dimensionless;


  // Scaling factors
  scale_gly = 12.5 dimensionless;
  scale_glyglc = 12.5 dimensionless;
  
  // Reactions
  GLUT2 is "GLUT2 glucose transporter";
  GLUT2: glc_ext -> glc;
  GLUT2 has mmol_per_s;
  GLUT2_keq = 1 dimensionless;
  GLUT2_km = 42 mM;
  GLUT2_Vmax = 420 mmol_per_s;
  GLUT2 = scale_gly * GLUT2_Vmax/GLUT2_km * (glc_ext - glc/GLUT2_keq)/(1 + glc_ext/GLUT2_km + glc/GLUT2_km);
  GLUT2 has mmol_per_s;

  GK is "Glucokinase";
  GK: glc + atp => glc6p + adp;
  GK has mmol_per_s;
  GK_n_gkrp = 2 dimensionless;
  GK_km_glc1 = 15 mM;
  GK_km_fru6p = 0.010 mM;
  GK_b = 0.7 dimensionless;
  GK_n = 1.6 dimensionless;
  GK_km_glc = 7.5 mM;
  GK_km_atp = 0.26 mM;
  GK_Vmax = 25.2 mmol_per_s;
  GK_gc_free = (glc^GK_n_gkrp / (glc^GK_n_gkrp + GK_km_glc1^GK_n_gkrp) ) * (1 - GK_b*fru6p/(fru6p + GK_km_fru6p));
  GK = scale_gly * GK_Vmax * GK_gc_free * atp/(GK_km_atp + atp) * glc^GK_n/(glc^GK_n + GK_km_glc^GK_n);
  GK_gc_free has dimensionless;
  GK has mmol_per_s;

  G6PASE is "D-Glucose-6-phosphate Phosphatase";
  G6PASE: glc6p + h2o => glc + phos;
  G6PASE_km_glc6p = 2 mM;
  G6PASE_Vmax =  18.9 mmol_per_s;
  G6PASE = scale_gly * G6PASE_Vmax * glc6p / (G6PASE_km_glc6p + glc6p);
  G6PASE has mmol_per_s;
 
  GPI is "D-Glucose-6-phosphate Isomerase";
  GPI: glc6p -> fru6P;
  GPI_keq = 0.517060817492925 dimensionless;
  GPI_km_glc6p  = 0.182 mM;
  GPI_km_fru6p = 0.071 mM;
  GPI_Vmax = 420 mmol_per_s;
  GPI = scale_gly * GPI_Vmax/GPI_km_glc6p * (glc6p - fru6p/GPI_keq) / (1 + glc6p/GPI_km_glc6p + fru6p/GPI_km_fru6p);
  GPI has mmol_per_s;

  G16PI is "Glucose 1-phosphate 1,6-phosphomutase";
  G16PI : glc1p -> glc6p;
  G16PI_keq = 15.717554082151441 dimensionless;
  G16PI_km_glc6p  = 0.67 mM;
  G16PI_km_glc1p = 0.045 mM;
  G16PI_Vmax = 100 mmol_per_s;
  G16PI = scale_glyglc * G16PI_Vmax/G16PI_km_glc1p * (glc1p - glc6p/G16PI_keq) / (1 + glc1p/G16PI_km_glc1p + glc6p/G16PI_km_glc6p);
  G16PI has mmol_per_s;

  UPGASE is "UTP:Glucose-1-phosphate uridylyltransferase";
  UPGASE : utp + glc1P -> udpglc + pp;
  UPGASE_keq = 0.312237619153088 dimensionless;
  UPGASE_km_utp = 0.563 mM;
  UPGASE_km_glc1p = 0.172 mM;
  UPGASE_km_udpglc = 0.049 mM;
  UPGASE_km_pp = 0.166 mM;
  UPGASE_Vmax = 80 mmol_per_s;
  UPGASE = scale_glyglc * UPGASE_Vmax/(UPGASE_km_utp*UPGASE_km_glc1p) * (utp*glc1p - udpglc*pp/UPGASE_keq) / ( (1 + utp/UPGASE_km_utp)*(1 + glc1p/UPGASE_km_glc1p) + (1 + udpglc/UPGASE_km_udpglc)*(1 + pp/UPGASE_km_pp) - 1 );
  UPGASE has mmol_per_s;
  
  PPASE is "Pyrophosphate phosphohydrolase";
  PPASE : pp + h2o => 2 phos;
  PPASE_km_pp = 0.005 mM;
  PPASE_Vmax = 2.4 mmol_per_s;
  PPASE = scale_glyglc * PPASE_Vmax * pp/(pp + PPASE_km_pp);
  PPASE has mmol_per_s;   


  GS is "Glycogen synthase";
  GS : udpglc => udp + glyglc;
  GS_C = 500 mM;
  GS_k1_max = 0.2 dimensionless;
  GS_k1_nat = 0.224 mMmM;
  GS_k2_nat = 0.1504 mM;
  GS_k1_phospho = 3.003 mMmM;
  GS_k2_phospho = 0.09029 mM;
  GS_Vmax =  13.2 mmol_per_s;
  
  GS_storage_factor = (1+GS_k1_max) * (GS_C - glyglc)/( (GS_C - glyglc) + GS_k1_max * GS_C);
  GS_k_udpglc_native = GS_k1_nat / (glc6p + GS_k2_nat);
  GS_k_udpglc_phospho = GS_k1_phospho / (glc6p + GS_k2_phospho);
  GS_native = scale_glyglc * GS_Vmax * GS_storage_factor * udpglc / (GS_k_udpglc_native + udpglc);
  GS_phospho = scale_glyglc * GS_Vmax * GS_storage_factor * udpglc / (GS_k_udpglc_phospho + udpglc); 
  GS = (1-gamma)*GS_native + gamma*GS_phospho;
  
  GS_storage_factor has dimensionless;
  GS_k_udpglc_native has mM;
  GS_k_udpglc_phospho has mM;
  GS_native has mmol_per_s;
  GS_phospho has mmol_per_s;
  GS has mmol_per_s;

 
  GP is "Glycogen-Phosphorylase";
  GP : glyglc + phos -> glc1p;
  GP_keq = 0.211826505793075 per_mM;
  GP_k_glyc_native = 4.8 mM;
  GP_k_glyc_phospho = 2.7 mM;
  GP_k_glc1p_native = 120 mM;
  GP_k_glc1p_phospho = 2 mM;
  GP_k_p_native = 300 mM;
  GP_k_p_phospho = 5 mM;
  GP_ki_glc_phospho = 5 mM;
  GP_ka_amp_native = 1 mM;
  GP_base_amp_native = 0.03 mmol_per_s;
  GP_max_amp_native = 0.30 mmol_per_s;
  GP_Vmax = 6.8 mmol_per_s;
  
  GP_C = GS_C;
  GP_k1_max = GS_k1_max;
  GP_fmax = (1+GP_k1_max) * glyglc /( glyglc + GP_k1_max * GP_C);
  GP_vmax_native = scale_glyglc * GP_Vmax * GP_fmax * (GP_base_amp_native + (GP_max_amp_native - GP_base_amp_native) *amp/(amp+GP_ka_amp_native));
  GP_native = GP_vmax_native/(GP_k_glyc_native*GP_k_p_native) * (glyglc*phos - glc1p/GP_keq) / ( (1 + glyglc/GP_k_glyc_native)*(1 + phos/GP_k_p_native) + (1 + glc1p/GP_k_glc1p_native)  - 1 );
  GP_vmax_phospho = scale_glyglc * GP_Vmax * GP_fmax * exp(-log(2)/GP_ki_glc_phospho * glc);
  GP_phospho = GP_vmax_phospho/(GP_k_glyc_phospho*GP_k_p_phospho) * (glyglc*phos - glc1p/GP_keq) / ( (1 + glyglc/GP_k_glyc_phospho)*(1 + phos/GP_k_p_phospho) + (1 + glc1p/GP_k_glc1p_phospho)  - 1);
  GP = (1-gamma) * GP_native + gamma*GP_phospho;

  GP_C has mM;
  GP_k1_max has dimensionless;
  GP_fmax has dimensionless;
  GP_vmax_native has mmol_per_s;
  GP_native has mmol_per_s;
  GP_vmax_phospho has mmol_per_s;
  GP_phospho has mmol_per_s;
  GP has mmol_per_s;

  NDKGTP is "Nucleoside-diphosphate kinase (ATP, GTP)";
  NDKGTP : atp + gdp -> adp + gtp;
  NDKGTP_keq = 1 dimensionless;
  NDKGTP_km_atp = 1.33 mM;
  NDKGTP_km_adp = 0.042 mM;
  NDKGTP_km_gtp = 0.15 mM;
  NDKGTP_km_gdp = 0.031 mM;
  NDKGTP_Vmax = 0 mM;
  NDKGTP = scale_gly * NDKGTP_Vmax / (NDKGTP_km_atp * NDKGTP_km_gdp) * (atp*gdp - adp*gtp/NDKGTP_keq) / ( (1 + atp/NDKGTP_km_atp)*(1 + gdp/NDKGTP_km_gdp) + (1 + adp/NDKGTP_km_adp)*(1 + gtp/NDKGTP_km_gtp) - 1);
  NDKGTP has mmol_per_s;


  NDKUTP is "Nucleoside-diphosphate kinase (ATP, UTP)";
  NDKUTP : atp + udp -> adp + utp;
  NDKUTP_keq = 1 dimensionless;
  NDKUTP_km_atp = 1.33 mM;
  NDKUTP_km_adp = 0.042 mM;
  NDKUTP_km_utp = 16 mM;
  NDKUTP_km_udp = 0.19 mM;
  NDKUTP_Vmax = 2940 mmol_per_s;
  NDKUTP = scale_glyglc * NDKUTP_Vmax / (NDKUTP_km_atp * NDKUTP_km_udp) * (atp*udp - adp*utp/NDKUTP_keq) / ( (1 + atp/NDKUTP_km_atp)*(1 + udp/NDKUTP_km_udp) + (1 + adp/NDKUTP_km_adp)*(1 + utp/NDKUTP_km_utp) - 1);
  NDKUTP has mmol_per_s;


  AK is "ATP:AMP phosphotransferase (Adenylatkinase)";
  AK : atp + amp -> 2 adp;
  AK_keq = 0.247390074904985 dimensionless; 
  AK_km_atp = 0.09 mM;
  AK_km_amp = 0.08 mM;
  AK_km_adp = 0.11 mM;
  AK_Vmax = 0 mmol_per_s;
  AK = scale_gly * AK_Vmax / (AK_km_atp * AK_km_amp) * (atp*amp - adp*adp/AK_keq) / ( (1+atp/AK_km_atp)*(1+amp/AK_km_amp) + (1+adp/AK_km_adp)*(1+adp/AK_km_adp) - 1); 
  AK has mmol_per_s;


  PFK2 is "ATP:D-fructose-6-phosphate 2-phosphotransferase";
  PFK2 : fru6p + atp => fru26bp + adp;
  PFK2_n_native = 1.3 dimensionless;       
  PFK2_n_phospho = 2.1 dimensionless;
  PFK2_k_fru6p_native = 0.016 mM; 
  PFK2_k_fru6p_phospho = 0.050 mM;
  PFK2_k_atp_native = 0.28 mM;
  PFK2_k_atp_phospho = 0.65 mM;
  PFK2_Vmax =  0.0042 mmol_per_s;

  PFK2_native = scale_gly * PFK2_Vmax * fru6p^PFK2_n_native / (fru6p^PFK2_n_native + PFK2_k_fru6p_native^PFK2_n_native) * atp/(atp + PFK2_k_atp_native);
  PFK2_phospho = scale_gly * PFK2_Vmax * fru6p^PFK2_n_phospho / (fru6p^PFK2_n_phospho + PFK2_k_fru6p_phospho^PFK2_n_phospho) * atp/(atp + PFK2_k_atp_phospho);
  PFK2 = (1-gamma) * PFK2_native + gamma*PFK2_phospho;
  PFK2_native has mmol_per_s;
  PFK2_phospho has mmol_per_s;
  PFK2 has mmol_per_s;


// 'FBP2'            % v14
// 'PFK1'            % v15
// 'FBP1'            % v16
// 'ALD'             % v17
// 'TPI'             % v18
// 'GAPDH'           % v19
// 'PGK'             % v20
// 'PGM'             % v21
// 'EN'              % v22
// 'PK'              % v23
// 'PEPCK'      % v24
// 'PEPCKMITO'      % v25
// 'PC'              % v26
// 'LDH'             % v27

// 'LACT'            % v28
// 'PYRT'            % v29
// 'PEPT'            % v30

// 'PDH'             % v31
// 'CS'              % v32
// 'NDKMITO'        % v33

// 'oaa_{flx}'         % v34
// 'acoa_{flx}'        % v35
// 'vcit_{flx}'        % v36

end
